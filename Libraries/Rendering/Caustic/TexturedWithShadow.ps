//**********************************************************************
// Copyright Patrick Sweeney 2015-2020
// Licensed under the MIT license.
// See file LICENSE for details.
// File: TexturedWithShadow.ps
//**********************************************************************
#define MAX_LIGHTS 4

struct VSOutput
{
    float4 posPS : SV_POSITION; // Position of vertex in projected coordinates
    float3 posWS : TEXCOORD0;
    float3 normWS : TEXCOORD1; // Normal vector in world coordinates
    float2 uvs : TEXCOORD2; // UV coordinates
    float4 lightPS[MAX_LIGHTS] : TEXCOORD3;
};

Texture2D diffuseTexture : register(t0);
SamplerState diffuseSampler : register(s0);
Texture2D shadowMapTexture : register(t1);
SamplerState shadowMapSampler : register(s1);

cbuffer PS_CONSTANT_BUFFER
{
    float4 lightPos;
    float4 lightColor[MAX_LIGHTS];
    float4 ambientColor;
    float4 diffuseColor;
    float4 specularColor;
    float4 specularExp;
    float4 lightPosWS[MAX_LIGHTS];
    float4 transparency;
    matrix viewInv; // View => World
    int numLights;
    float4 shadowMapBounds[MAX_LIGHTS];
};

float4 PS(VSOutput p) : SV_Target
{
    float3 viewPosWS = mul(float4(0.0f, 0.0f, 0.0f, 1.0f), viewInv).xyz;
    float3 viewDirWS = normalize(viewPosWS - p.posWS);
    float3 color = float3(0.0f, 0.0f, 0.0f);
    float3 normWS = normalize(p.normWS);

    // Check if in shadow. For now only assume 1 light is shadow caster
    float shadowDist = shadowMapTexture.Sample(shadowMapSampler, p.lightPS[0].xy).x;
    float diffuseAttenuation = 1.0f;
    float specularAttenuation = 1.0f;
    if (p.lightPS[0].z > shadowDist)
    {
        diffuseAttenuation = 0.5f;
        specularAttenuation = 0.0f;
    }

    for (int i = 0; i < numLights; i++)
    {
        float3 lightDirWS = normalize(lightPosWS[i].xyz - p.posWS);

        // L.N
        float dp = diffuseAttenuation * saturate(dot(normWS, lightDirWS));
        
        // Diffuse
        float4 clr = diffuseTexture.Sample(diffuseSampler, p.uvs);
        color.xyz += clr.zyx * dp * lightColor[i].xyz;

        // specular
        float3 reflectWS = reflect(lightDirWS, normWS);
        float specdp = saturate(dot(reflectWS, -viewDirWS));
        float spec = specularAttenuation * pow(specdp, specularExp.x);
        color.xyz += specularColor.xyz * spec * lightColor[i].xyz;
    }
    color.xyz += ambientColor.xyz;
    return float4(color.xyz, transparency.x);
}
