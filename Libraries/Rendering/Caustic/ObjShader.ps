//**********************************************************************
// Copyright Patrick Sweeney 2021
// Licensed under the MIT license.
// See file LICENSE for details.
// File: ObjShader.cs
//**********************************************************************
#include "defs.h"

Texture2D diffuseTexture : register(t0);
SamplerState diffuseSampler : register(s0);
Texture2D shadowMapTexture : register(t1);
SamplerState shadowMapSampler : register(s1);
Texture2D transparencyTexture : register(t2);
SamplerState transparencySampler : register(s2);

cbuffer PS_CONSTANT_BUFFER
{
    float4 lightPos;
    float4 lightColor[MAX_LIGHTS];
    float4 ambientColor;
    float4 diffuseColor;
    float4 specularColor;
    float4 specularExp;
    float4 lightPosWS[MAX_LIGHTS];
    float4 transparency;
    matrix viewInv; // View => World
    int numLights;
    float4 shadowMapBounds;
    float transparencyAvail;
};

float4 PS(VSOutput p) : SV_Target
{
    float3 viewPosWS = mul(float4(0.0f, 0.0f, 0.0f, 1.0f), viewInv).xyz;
    float3 viewDirWS = normalize(viewPosWS - p.posWS);
    float3 color = float3(0.0f, 0.0f, 0.0f);
    float3 normWS = normalize(p.normWS);

    // Check if point is in shadow
    //float shadowDist = shadowMapTexture.Sample(shadowMapSampler, p.uvs);

    float4 clr = diffuseTexture.Sample(diffuseSampler, p.uvs);
    clr.rgb += diffuseColor.rgb;
    float t = (transparencyAvail > 0.0f) ? transparencyTexture.Sample(transparencySampler, p.uvs).x : 1.0f;
    for (int i = 0; i < numLights; i++)
    {
        float3 lightDirWS = normalize(lightPosWS[i].xyz - p.posWS);

        // L.N
        float dp = saturate(dot(normWS, lightDirWS));
        
        // Diffuse
        color.xyz += clr.xyz * dp * lightColor[i].xyz;

        // specular
        float3 reflectWS = reflect(lightDirWS, normWS);
        float specdp = saturate(dot(reflectWS, -viewDirWS));
        float spec = pow(specdp, specularExp.x);
        color.xyz += specularColor.xyz * spec * lightColor[i].xyz;
    }
    color.xyz += ambientColor.xyz;
    return float4(color.xyz, t);
}
